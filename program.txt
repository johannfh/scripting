fn create_node(title) {
    return {{ title: title, next: undefined }};
}

fn create_queue() {
    return {{ head: undefined, tail: undefined }};
}

fn queue_enq(queue, node) {
    if !queue.head {
        queue.head = node;
        queue.tail = node;
    } else {
        queue.tail.next = node;
        queue.tail = node;
    }
}

fn queue_deq(queue) {
    if !queue.head {
        return null;
    } else {
        let result = queue.head;
        queue.head = result.next;
        if !queue.head {
            queue.tail = null;
        }
        return result;
    }
}

fn queue_show(queue) {
    let current = queue.head;
    let i = 1;
    while current {
        print(i + ". > " + current.title);
        current = current.next;
        i = i + 1;
    }
}

let tq = create_queue();

fn print_help() {
    print("-- List of commands --");
    print("h/help: show this menu");
    print("a/add: add a task");
    print("d/done: complete the next task");
    print("s/show: show all tasks");
    print("q/quit: quit the program");
    print("c/clear: clear the screen");
}

fn add_command() {
    let title = input("Enter title: ");
    print("Adding task " + title);
    let node = create_node(title);
    queue_enq(tq, node);
}

fn done_command() {
    let node = queue_deq(tq);
    if !node {
        print("Todo list already empty");
    } else {
        print("Marked '" + node.title + "' as completed!");
    }
}

fn show_command() {
    print("-- Todo List --");
    queue_show(tq);
}

let running = true;
while running {
    let command = input("Enter command (h for help): ");
    if command == "h" || command == "help" {
        print_help();
        continue;
    }
    if command == "a" || command == "add" {
        add_command();
        continue;
    }
    if command == "d" || command == "done" {
        done_command();
        continue;
    }
    if command == "s" || command == "show" {
        show_command();
        continue;
    }
    if command == "q" || command == "quit" {
        running = false;
        continue;
    }
    if command == "c" || command == "clear" {
        clear();
        continue;
    }

    print("unknown command: " + command);
}

print("Goodbye!");
